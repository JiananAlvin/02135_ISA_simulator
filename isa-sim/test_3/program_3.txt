# Program to insertion sort an array of 10 values
# Check the expected output and compare it with the one of your simulator
# The format is: <instruction mem. address>: <OPCODE> <COMMA SEPARATED OPERANDS>;
# All numers are decimal 

# Copy the array from source (0-9) to destination (10-19)
0: LI R1, 0;  # R1 is the read pointer
1: LI R2, 10;  # R2 is the write pointer
2: LI R3, 1;  # R3 is the unit to increment or decrememnt
3: LI R4, 9;  # R4 is the counter for copy
# R5 is the temporary buffer for copy
4: LI R6, 6;  # Copy address
5: LI R7, 13;  # Done copy address

# copy:
6: LD R5, R1;  # Read form mem(read pointer)
7: SD R5, R2;  # Write to mem(write pointer)
8: JEQ R7, R4, R0;  # Goto done: if the counter is zero
9: SUB R4, R4, R3;  # Decrement counter
10: ADD R1, R1, R3;  # Increment read pointer
11: ADD R2, R2, R3;  # Increment write pointer
12: JR R6;  # Goto copy:

# Preparation before sorting:
13: LI R7, 20;  # Insertion sort address
14: LI R8, 29;  # No swap address
15: LI R10, 10;  # upper boundary
16: LI R9, 20;  # lower boundary
17: LI R4, 0;  # the accumulator of increment value for pointer 1 and pointer 2

# 
18: LI R1, 10;  # R1 is the read/write pointer 1
19: LI R2, 11;  # R2 is the read/write pointer 2
# R5, R6 are the temporary buffers for copy

# insertion sort:
20: LD R5, R1;  # Read form mem(read pointer 1)
21: LD R6, R2;  # Read form mem(read pointer 2)
22: JLT R8, R5, R6;  # If R5<R6, no swap
23: SD R5, R2;  # Write to mem(write pointer 2)
24: SD R6, R1;  # Write to mem(write pointer 1)
25: JEQ R8, R1, R10;  # upper boundary is reached, Goto no swap
26: SUB R1, R1, R3;  # Decrement point 1 by 1
27: SUB R2, R2, R3;  # Decrement point 2 by 1
28: JR R7;  # Goto insertion sort (starting the new round)

# no swap:
29: ADD R4, R4, R3;  # accumulate the increment value by 1
30: LI R1, 10; # reset pointer 1 to 10
31: LI R2, 11; # reset pointer 2 to 11
32: ADD R1, R1, R4;  # Increment pointer 1 by accumulator
33: ADD R2, R2, R4;  # Increment pointer 2 by accumulator
34: JLT R7, R2, R9;  # Repeat(new round) if pointer 2 is less than 20
35: END;
